buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.google.com' }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'com.bmuschko.nexus'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 3
        versionName "2.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.google.dagger:dagger:2.19'
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.1.0"
}

// project information
group = 'com.onepointsixtwo'
version = '2.0.0'

project.ext {
    nexusUsername = ossrhUser
    nexusPassword = ossrhPassword
}

modifyPom {
    project {
        name 'dagger_viewmodel_android'
        description 'The Android part of the dagger_viewmodel lib'
        url 'https://github.com/onepointsixtwo/dagger_viewmodel_android'
        groupId = 'com.onepointsixtwo'

        scm {
            url 'https://github.com/onepointsixtwo/dagger_viewmodel_android'
            connection 'scm:github.com/onepointsixtwo/dagger_viewmodel_android'
            developerConnection 'scm:git:git@github.com:onepointsixtwo/dagger_viewmodel_android.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'http://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id = 'onepointsixtwo'
                name = 'John Kartupelis'
                email = 'john.kartupelis@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}
